
/*****************************************************************************/

LABEL @CodeExtractExitLabel(word P);

LABEL @CodeExtractJump(word P)
	/* Skip `pass` statements, if any. */
	while Node[P].ID == NID.iEMPTY & Node[P].pRight != nNODE loop
		P = Node[P].pRight;
	end:while

	switch Node[P].ID of
	case NID.iVIRTUAL_JUMP:
		return @LabelResolve(Node[P].Value, Node[P].pType);
	case NID.iCONTINUE:
		return @LabelResolve(Node[P].Value, JmpLbl.lCONTINUE);
	case NID.iEXIT, NID.iVIRTUAL_EXIT:
		return @CodeExtractExitLabel(Node[P].Value);
	default:
		return NULL;
	end:switch
end

LABEL @CodeExtractExitLabel(word P)
	LABEL @L_Exit = NULL;
	when Node[P].pRight != nNODE:
		@L_Exit = @CodeExtractJump(Node[P].pRight);
	when @L_Exit == NULL:
		@L_Exit = @LabelResolve(P, JmpLbl.lEXIT);
	return @L_Exit;
end

void CodeList_InsertVirtualJump(word P; word pTarget; word LabelId)
	word pJump = Peek();
	Node[pJump].ID = NID.iVIRTUAL_JUMP;
	Node[pJump].Value = pTarget;
	Node[pJump].pType = LabelId;
	Node[pJump].SourceLocation.Line = -1;

	while Node[P].pRight != nNODE do
		P = Node[P].pRight;
	end
	Node[P].pRight = pJump;
end

void CodeList_InsertVirtualExit(word P; word pTarget)
	word pJump = Peek();
	Node[pJump].ID = NID.iVIRTUAL_EXIT;
	Node[pJump].Value = pTarget;
	Node[pJump].SourceLocation.Line = -1;

	while Node[P].pRight != nNODE do
		P = Node[P].pRight;
	end
	Node[P].pRight = pJump;
end

/*****************************************************************************/

word Code_OR(word P; LABEL @F; LABEL @T; LABEL @M; char @Buff)
	LABEL @L = @LabelResolve(P, JmpLbl.lBRANCH);
	if @T == NULL then
		CodeBooleanExpr(Node[P].pLeft , NULL, @M, @L);
		EmitLabelIfUsed(@L);
		CodeBooleanExpr(Node[P].pRight, @F, NULL, @M);
	else
		CodeBooleanExpr(Node[P].pLeft , NULL, @T, @L);
		EmitLabelIfUsed(@L);
		CodeBooleanExpr(Node[P].pRight, NULL, @T, @M);
	end
	return st_cond_branch;
end

/*****************************************************************************/

word Code_XOR(word P; LABEL @F; LABEL @T; LABEL @M; char @Buff)
	Stop(@L10n.eXORNOTIMPL);
end

/*****************************************************************************/

word Code_EQV(word P; LABEL @F; LABEL @T; LABEL @M; char @Buff)
	Stop(@L10n.eEQVNOTIMPL);
end

/*****************************************************************************/

word Code_AND(word P; LABEL @F; LABEL @T; LABEL @M; char @Buff)
	LABEL @L = @LabelResolve(P, JmpLbl.lBRANCH);
	if @T == NULL then
		CodeBooleanExpr(Node[P].pLeft , @F, NULL, @L);
		EmitLabelIfUsed(@L);
		CodeBooleanExpr(Node[P].pRight, @F, NULL, @M);
	else
		CodeBooleanExpr(Node[P].pLeft , @M, NULL, @L);
		EmitLabelIfUsed(@L);
		CodeBooleanExpr(Node[P].pRight, NULL, @T, @M);
	end
	return st_cond_branch;
end

/*****************************************************************************/

word Code_PUSH(word P; char @Buff)

	if Node[P].pRight<nNODE then
		Code_(Node[P].pRight, @Buff);
	end

	word P1 = Node[P].pLeft;

	word Size = SizeOfType(Node[P1].pType);
	select
	case Size==1 | Size==2 | Size==4:
		word Reg = LookupRegForNode(P1);
		if Reg != RegNone then
			Buff[0] = #0;
			Emit_PUSH_Reg(Reg, 4);
			return nDICT;
		end:if
	end:select

	/* Оптимизация прямой загрузки значения из переменной. */
	char BuffOperand[nBUFF];
	word pOperandType;
	if CodeAsMemoryOperand(@BuffOperand, P1, @pOperandType) & (T_SizeOf(pOperandType) ==  4) then
		EmitWithPhoMode(@strcpy2(@Buff, "push    ", @BuffOperand), PHO_Mode_Skip);
		return nDICT;
	end

	/* Оптимизация прямой загрузки указателя на строку. */
	if
		(Node[P1].ID == NID.iLPTR) &
		(Node[P1].pLeft != nNODE) &
		(Node[Node[P1].pLeft].ID == NID.iSTRING)
	then
		ValueRef Ref;
		ValueRef_ExtractDataInfo(@Ref, Node[P1].pLeft);
		char @s = "dword @@ROLITERALS+";
		when !Ref.RO:
			@s = "dword @@RWLITERALS+";
		EmitPhoSkip(@strcpy3(@Buff, "push    ",@s,@str(Ref.Offset)));
		return nDICT;
	end:if

	/* Оптимизация прямой загрузки числа. */
	if NodeCanBeEncodedAsImm(P1) then
		word Value = Node[P1].Value;

		if Size == 4 | Size == 2 | Size == 1 then
			/* Если какой-либо регистр уже содержит нужную константу... */
			/* FIXME: проверять только часть регистра при необходимости */
			word Reg = PHO_FindRegWithConst(Value);
			if Reg != PHO_REG_INVALID then
				EmitPhoSkip(@strcpy2(@Buff, "push    ", @PHO_EncodeReg(Reg, 0xFFFF_FFFF)));
				return nDICT;
			end:if
		end:if

		/* Прямая загрузка числа. Кроме значения 0.
		   Для 0 используем общий алгоритм, который позволяет оптимизировать до xor.
		*/
		if Value != 0 then
			if Size == 4 | Size == 2 | Size == 1 then
				EmitPhoSkip(@strcpy2(@Buff, "push    dword ", @str(Value)));
				return nDICT;
			end:if
		end:if
	end:if

	/* Все остальные варианты... */

	Reg_TARGET = RegA;
	word pType = Code_(P1, @Buff);
	when pType != Node[P1].pType:
		StopAtNodeInternalCodegen(__FILE__, __LINE__, P);

	select
	case Size==1 | Size==2 | Size==4:
		Emit_PUSH_Reg(Reg_TARGET, 4);
	default:
		char @TX = @EncodeReg(NULL, Reg_TARGET, 4);
		Emit(@strcpy2(@Buff,"sub     ESP,  ",@str(DefaultInStackAlign(Size))));
		Emit(@strcpy2(@Buff,"mov     ESI,  ",@TX));
		Emit("mov     EDI,  ESP");
		Emit_MOVS(Size);
	end:select

	return nDICT;
end

/*****************************************************************************/

word Code_RETURN(word P; char @Buff)

	if Node[P].pLeft<nNODE then
		word Saved_Reg_TARGET_Locked = Reg_TARGET_Locked;
		inc Reg_TARGET_Locked;
		Reg_ALT_TARGET = RegNone;
		Reg_TARGET = RegA;
		word pType = Code_(Node[P].pLeft, @Buff);
		if Reg_TARGET != RegA then
			Emit_MOV_or_XCHG(RegA, Reg_TARGET, 4);
		end:if
		Reg_TARGET_Locked = Saved_Reg_TARGET_Locked;
	end:if

	if Dict[FnCtx.pFunc].Sub == SUBCLASS.sSTART_FUNC then
		select
		case TargetFileFormat == TARGET_FORMAT.tPE:
			when pExitProcess == nDICT:
				StopAtNodeInternalCodegen(__FILE__, __LINE__, P);
			Emit("xor     EAX,  EAX");
			Emit("push    EAX");
			Emit(@strcpy3(@Buff,"call   [@",@str(Dict[pExitProcess].Label),"]"));
		case TargetFileFormat == TARGET_FORMAT.tELF:
			Emit("mov     EAX,  1");
			Emit("xor     EBX,  EBX");
			Emit("int     80H");
		default:
			StopAtNodeInternalCodegen(__FILE__, __LINE__, P);
		end:select
	else
		EmitFunctionEpilogue(@Buff, @FnCtx);
	end:if

	return st_noreturn;
end

/*****************************************************************************/

bool ConsiderColdSeperation(word pCond; word pBody)
	when (!SwOptions.SeparateColdBranches)
	|    (!NodeIsUnlikely(pCond))
	|    (Emit_ColdRedirectionActive())
	|    (!Emit_ColdRedirectionAllowed())
	|    (NodeGetTreeSize(pBody) > 128):
		return false;
	return true;
end

word Code_SELECT(word P; char @Buff)

	/*
	                 iSELECT
	                /       \
	              iCASE      [next statement]
	             /     \
	            iCOND   [next case]
	           /     \
	[condition]     iBODY
	               /    \
	         [body]     NULL
	*/

	LABEL @L_Exit = @CodeExtractExitLabel(P);

	word pTypeBody = st_void;
	bool CondFallsThroughOnFalse = true;
	word pTypeFinal = st_noreturn;
	word CASE = Node[P].pLeft;
	word LAST_CASE = CASE;
	LABEL @L_JumpOnTrue = NULL;
	while CASE < nNODE do
		when Node[CASE].ID != NID.iCASE:
			StopInternalCodegen(__FILE__, __LINE__);

		word COND = Node[CASE].pLeft;
		word BODY = Node[COND].pRight;

		when Node[BODY].ID != NID.iBODY
		|    Node[COND].ID != NID.iCOND:
			StopInternalCodegen(__FILE__, __LINE__);

		bool ThisCaseIsLast = (Node[CASE].pRight == nNODE);

		LABEL @L_CASEBODYEND = NULL;
		if ThisCaseIsLast then
			@L_CASEBODYEND = @L_Exit;
		else
			@L_CASEBODYEND = @LabelResolve(CASE, JmpLbl.lCASEBODYEND);
		end:if

		bool ColdSeparation = false;
		bool BodyEliminated = false;
		@L_JumpOnTrue = NULL;

		if Node[COND].pLeft < nNODE then
			@L_JumpOnTrue = @CodeExtractJump(Node[BODY].pLeft);
			if @L_JumpOnTrue != NULL then
				BodyEliminated = true;
			else
				if ThisCaseIsLast & (@L_Exit != @LabelResolve(P, JmpLbl.lEXIT)) then
					/*
						Проверка для случая наподобие:
							while ... loop
								...
								when x:
									panic(...);
							end:while
						В этом случае выгоднее оставить холодную ветвь на месте,
						поскольку уход в следующую итерацию цикла выполняется
						непосредственно условными ветвлениями при проверке условия.
					*/
					pass;
				else
					ColdSeparation = ConsiderColdSeperation(Node[COND].pLeft, Node[BODY].pLeft);
					when ColdSeparation:
						@L_JumpOnTrue = @LabelResolve(CASE, JmpLbl.lCASEBODY);
				end:if
			end

			word pCondType;
			if @L_JumpOnTrue == NULL then
				pCondType = CodeBooleanExpr(Node[COND].pLeft,
					@L_CASEBODYEND,NULL,@LabelResolve(CASE, JmpLbl.lCASEBODY));
				CondFallsThroughOnFalse = false;
			else
				pCondType = CodeBooleanExpr(Node[COND].pLeft,
					NULL,@L_JumpOnTrue,@L_CASEBODYEND);
				CondFallsThroughOnFalse = true;
			end:if
			when pCondType != st_cond_branch:
				StopAtNodeInternalCodegen(__FILE__, __LINE__, P);
		else
			CondFallsThroughOnFalse = false;
		end

		if BodyEliminated then
			pTypeBody = st_noreturn;
		else
			CodeList_InsertVirtualExit(Node[BODY].pLeft, P);
			PHO_STATE State;
			PHO_SaveState(@State);
			if ColdSeparation then
				Emit_StartColdRedirection();
			end:if
			EmitLabelIfUsed(@LabelResolve(CASE, JmpLbl.lCASEBODY));
			pTypeBody = Code_Block(Node[BODY].pLeft, @Buff);
			if pTypeBody != st_noreturn then
				if !ThisCaseIsLast | ColdSeparation then
					EmitJumpToLabel(@L_Exit);
				end:if
			end:if
			if ColdSeparation then
				Emit_StopColdRedirection();
			end:if
			if pTypeBody == st_noreturn then
				PHO_RestoreState(@State);
			end:if
		end:if

		EmitLabelIfUsed(@LabelResolve(CASE, JmpLbl.lCASEBODYEND));

		LAST_CASE = CASE;
		CASE = Node[CASE].pRight;
	end:while

	EmitLabelIfUsed(@LabelResolve(P, JmpLbl.lEXIT));

	if
		/* The last BODY cannot fall through. */
		(pTypeBody == st_noreturn) &
		/* The last COND cannot fall through. */
		(CondFallsThroughOnFalse == false) &
		/* No jumps go here. */
		!LabelIsUsed(@LabelResolve(P, JmpLbl.lEXIT)) &
		!LabelIsUsed(@LabelResolve(LAST_CASE, JmpLbl.lCASEBODYEND))
	then
		pTypeFinal = st_noreturn;
	else
		pTypeFinal = st_void;
	end:if

	return pTypeFinal;
end

/*****************************************************************************/

word Code_SWITCH_CASE_GetBody(word CASE_I)
	word  VALUE_I = Node[CASE_I].pLeft;
	while VALUE_I < nNode do
		switch Node[VALUE_I].ID of
		case NID.iVALUE, NID.iDEFAULT:
			pass;
		case NID.iBODY:
			return VALUE_I;
		default:
			StopInternalCodegen(__FILE__, __LINE__);
		end:switch
		VALUE_I = Node[VALUE_I].pRight;
	end:while
	StopInternalCodegen(__FILE__, __LINE__);
end

word Code_SWITCH(word P; char @Buff)

	/*
	                 iSWITCH
	                /       \
	              iCOND      [next statement]
	             /     \
	 [expression]     iCASE
	                 /     \
	            iVALUE      [next case]
	           /      \
	    [value]       iVALUE
                    /      \
	         [value]       iBODY
	                      /     \
	                [body]       NULL


                      iCASE
	                 /     \
	          iDEFAULT      [next case]
	         /        \
	     NULL        iBODY
	                /     \
	          [body]       NULL


	*/

	LABEL @L_Exit = @CodeExtractExitLabel(P);

	word pTypeFinal = st_noreturn;
	bool DefaultBranchPresent = false;

	word COND = Node[P].pLeft;
	when Node[COND].ID != NID.iCOND:
		StopInternalCodegen(__FILE__, __LINE__);

	if CodeRequiresSingleRegister(Node[COND].pLeft) then
		Reg_TARGET = ChooseEmptyReg2();
	else
		Reg_TARGET = RegA;
	end:if
	word pType = Code_(Node[COND].pLeft, @Buff);
	word Size = SizeOfType(pType);
	word Reg = Reg_TARGET;

	LABEL @L_Default = @L_Exit;
	word DEFAULT_CASE_I = nNODE;

	word offset = 0;

	PHO_STATE State;
	PHO_SaveState(@State);

	word  CASE_I = Node[COND].pRight;
	while CASE_I < nNODE do
		when Node[CASE_I].ID != NID.iCASE:
			StopInternalCodegen(__FILE__, __LINE__);

		word BODY_I = Code_SWITCH_CASE_GetBody(CASE_I);
		LABEL @L_Jump = @CodeExtractJump(Node[BODY_I].pLeft);
		when @L_Jump == NULL:
			@L_Jump = @LabelResolve(CASE_I, JmpLbl.lCASEBODY);

		word  VALUE_I = Node[CASE_I].pLeft;
		while VALUE_I < nNode do
			switch Node[VALUE_I].ID of
			case NID.iVALUE:
				word value = Node[Node[VALUE_I].pLeft].Value - offset;

				switch Size of
				case 1:
					value = value & 0xFF;
				case 2:
					value = value & 0xFFFF;
				case 4:
					pass;
				default:
					StopInternalCodegen(__FILE__, __LINE__);
				end:switch

				Buff[0] = #0;
				select
				case value == 0:
					Emit_TEST_Reg_Self(Reg, Size);
				case value == 1 & PreferIncDec(Reg, Size) > 0:
					Emit_DEC_Reg(Reg, Size);
				case value == 2 & PreferIncDec(Reg, Size) > 1:
					Emit_DEC_Reg(Reg, Size);
					Emit_DEC_Reg(Reg, Size);
				case value == word(-1) & PreferIncDec(Reg, Size) > 0:
					Emit_INC_Reg(Reg, Size);
				case value == word(-2) & PreferIncDec(Reg, Size) > 1:
					Emit_INC_Reg(Reg, Size);
					Emit_INC_Reg(Reg, Size);
				default:
					Emit(@EncodeCmdRegFmt(@Buff, "sub", Reg, Size, @str(value)));
				end:select

				offset = offset + value;

				EmitBranchToLabel("e", @L_Jump);
			case NID.iDEFAULT:
				@L_Default = @LabelResolve(CASE_I, JmpLbl.lCASEBODY);
				DefaultBranchPresent = true;
				DEFAULT_CASE_I = CASE_I;
			case NID.iBODY:
				pass;
			default:
				StopInternalCodegen(__FILE__, __LINE__);
			end:switch
			VALUE_I = Node[VALUE_I].pRight;
		end:while

		CASE_I = Node[CASE_I].pRight;
	end:while

	EmitJumpToLabel(@L_Default);

	CASE_I = Node[COND].pRight;
	while CASE_I < nNODE do
		word NEXT_CASE_I = Node[CASE_I].pRight;

		when Node[CASE_I].ID != NID.iCASE:
			StopInternalCodegen(__FILE__, __LINE__);

		word BODY_I = Code_SWITCH_CASE_GetBody(CASE_I);
		LABEL @L_Jump = @CodeExtractJump(Node[BODY_I].pLeft);

		word pTypeBody;
		if @L_Jump == NULL then
			EmitLabel(@LabelResolve(CASE_I, JmpLbl.lCASEBODY));
			PHO_RestoreState(@State);
			if CASE_I != DEFAULT_CASE_I & Size == 4 then
				PHO_RegLoadConst(Reg, 0);
			else
				PHO_InvalidateReg(Reg);
			end:if
			CodeList_InsertVirtualExit(Node[BODY_I].pLeft, P);
			pTypeBody = Code_Block(Node[BODY_I].pLeft, @Buff);
		else
			pTypeBody = st_noreturn;
		end:if

		if pTypeBody != st_noreturn then
			pTypeFinal = st_void;
			when NEXT_CASE_I < nNODE:
				EmitJumpToLabel(@L_Exit);
		end:if

		CASE_I = NEXT_CASE_I;
	end:while

	when !DefaultBranchPresent:
		pTypeFinal = st_void;

	EmitLabelIfUsed(@LabelResolve(P, JmpLbl.lEXIT));

	if (pTypeFinal == st_noreturn) & !LabelIsUsed(@LabelResolve(P, JmpLbl.lEXIT)) then
		pTypeFinal = st_noreturn;
	else
		pTypeFinal = st_void;
	end:if

	return pTypeFinal;
end

/*****************************************************************************/

word Code_WHILE(word P; char @Buff)

	/*
	                 iWHILE
	                /     \
	              iBODY    [next statement]
	             /    \
	  [loop body]    iCOND
	                /    \
	[loop condition]      NULL
	*/

	LABEL @L_Exit = @CodeExtractExitLabel(P);

	CODEBLOCK CB;
	Code_BeginBlock(@CB);

	word BODY = Node[P].pLeft;
	word COND = Node[BODY].pRight;

	when Node[BODY].ID != NID.iBODY
	|    Node[COND].ID != NID.iCOND:
		StopAtNodeInternalCodegen(__FILE__, __LINE__, P);

	Emit_ALIGN(SwOptions.AlignLoops);
	EmitLabel(@LabelResolve(P, JmpLbl.lCONTINUE));

	if Node[COND].pLeft < nNODE then
		when CodeBooleanExpr(
			Node[COND].pLeft, @L_Exit, NULL, @LabelResolve(P, JmpLbl.lLOOPBODY)
		) != st_cond_branch:
			StopAtNodeInternalCodegen(__FILE__, __LINE__, P);
		EmitLabelIfUsed(@LabelResolve(P, JmpLbl.lLOOPBODY));
	end

	CodeList_InsertVirtualJump(Node[BODY].pLeft, P, JmpLbl.lCONTINUE);
	word pTypeBody = Code_List(Node[BODY].pLeft, @Buff);
	when pTypeBody != st_noreturn:
		EmitJumpToLabelWithComment(@LabelResolve(P, JmpLbl.lCONTINUE), "while");

	EmitLabelIfUsed(@LabelResolve(P, JmpLbl.lEXIT));

	Code_EndBlock(@CB);

	when !LabelIsUsed(@LabelResolve(P, JmpLbl.lEXIT)):
		return st_noreturn;

	return Node[P].pType;
end

/*****************************************************************************/

word Code_REPEAT(word P; char @Buff)

	/*
	                 iREPEAT
	                /      \
	              iBODY     [next statement]
	             /    \
	  [loop body]    iCOND
	                /    \
	[loop condition]      NULL
	*/

	CODEBLOCK CB;
	Code_BeginBlock(@CB);

	word BODY = Node[P].pLeft;
	word COND = Node[BODY].pRight;

	when Node[BODY].ID != NID.iBODY
	|    Node[COND].ID != NID.iCOND:
		StopAtNodeInternalCodegen(__FILE__, __LINE__, P);

	/* Check the case when loop can be replaced with simple code block */
	if NodeIsTrue(Node[COND].pLeft) &
	   !CTE_LoopHasExits(P, Node[BODY].pLeft) &
	   !CTE_LoopHasContinues(P, Node[BODY].pLeft)
	then
		Code_List(Node[BODY].pLeft, @Buff);
		return st_void;
	end

	Emit_ALIGN(SwOptions.AlignLoops);
	EmitLabel(@LabelResolve(P, JmpLbl.lLOOPBODY));

	Code_List(Node[BODY].pLeft, @Buff);

	EmitLabelIfUsed(@LabelResolve(P, JmpLbl.lCONTINUE));

	if Node[COND].pLeft < nNODE then
		when CodeBooleanExpr(
			Node[COND].pLeft, @LabelResolve(P, JmpLbl.lLOOPBODY), NULL, @LabelResolve(P, JmpLbl.lEXIT)
		) != st_cond_branch:
			StopAtNodeInternalCodegen(__FILE__, __LINE__, P);
	end

	EmitLabelIfUsed(@LabelResolve(P, JmpLbl.lEXIT));

	Code_EndBlock(@CB);

	return Node[P].pType;
end

/*****************************************************************************/

word Code_CONTINUE(word P; char @Buff)
	EmitJumpToLabelWithComment(@CodeExtractJump(P), "continue");
	return st_noreturn;
end

/*****************************************************************************/

word Code_EXIT(word P; char @Buff)
	EmitJumpToLabelWithComment(@CodeExtractJump(P), "exit");
	return st_noreturn;
end

/*****************************************************************************/
