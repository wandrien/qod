
/*
	Анализатоp упpавляющих констpукций.
*/

/*****************************************************************************/

void MatchSemicolon(char @Buff)
	Scan(@Buff);
	if str_ne(@Buff,";") then
		Stop(@eSEMICOLONEXP);
	end
end

void MatchOptionalSemicolon(char @Buff)
	if str_eq(@Buff,";") then
		Scan(@Buff);
	end
end

/*****************************************************************************/

word EndOf(char @Buff, @A1, @A2; bool EatSemicolon)
	when str_ne(@Buff, "end"):
		StopInternal(__FILE__, __LINE__);

	Scan(@Buff);
	if EatSemicolon & str_eq(@Buff, ";") then
		return 0;
	end

	if str_eq(@Buff, ":") then
		Scan(@Buff);
		if str_eq(@Buff, @A1)
		| (@A2 != NULL & str_eq(@Buff, @A2)) then
			Scan(@Buff);
			if EatSemicolon & str_eq(@Buff, ";") then
				return 0;
			else
				return 1;
			end
		else
			Stop(@eNOTALLOWED);
		end:if
	end:if
	return 1;
end

/*****************************************************************************/

void Statement_IF(char @Buff; word @P1; word @SkipFinalScan)
	Node[P1].ID = iSELECT;

	char @End1="if";
	char @Case="";
	char @Then="then";
	char @Else="else";
	if str_eq(@Buff,"select") then
		@End1="select";
		@Case="case";
		@Then=":";
		@Else="default";

		if str_ne(@Scan(@Buff),"case") then
			Stop(@eCASEEXP);
		end:if
	else
		strcpy(@Buff,@Case);
	end:if

	word @BackLink = @Node[P1].pLeft;
	while str_ne(@Buff,"end") do
		word CASE = Peek();
		word COND = Peek();
		word BODY = Peek();

		Node[CASE].ID = iCASE;
		Node[COND].ID = iCOND;
		Node[BODY].ID = iBODY;

		Node[CASE].pLeft  = COND;
		Node[COND].pRight = BODY;
		BackLink = CASE;
		@BackLink = @Node[CASE].pRight;

		if str_eq(@Buff,@Case) then
			Node[COND].pLeft = Expr(PRIO_ZERO, @Scan(@Buff));
			word pType = Node[Node[COND].pLeft].pType;

			if str_ne(@Buff,@Then) then
				Stop(@eTHENEXP);
			end:if

			if pType != st_bool then
				ErrorAtNode(COND, @eTYPE_COND_EXPR);
			end:if
		else
			if str_eq(@Else,"default") then
				if str_ne(@Scan(@Buff),":") then
					Stop(@eCOLONEXP);
				end:if
			end:if

			@Case="";
			@Else="";
		end:if

		Scan(@Buff);
		Node[BODY].pLeft = Statements(@Buff, @Case, @Else, "end");
	end:while

	SkipFinalScan = EndOf(@Buff, @End1, NULL, true);

	CTE_SELECT(P1);
end

/*****************************************************************************/

void Statement_WHEN(char @Buff; word @P1; word @SkipFinalScan)

	Node[P1].ID=iSELECT;

	word CASE = Peek();
	word COND = Peek();
	word BODY = Peek();

	Node[CASE].ID = iCASE;
	Node[COND].ID = iCOND;
	Node[BODY].ID = iBODY;

	Node[CASE].pLeft  = COND;
	Node[COND].pRight = BODY;
	Node[P1].pLeft = CASE;

	Node[COND].pLeft = Expr(PRIO_ZERO, @Scan(@Buff));
	word pType = Node[Node[COND].pLeft].pType;

	if pType != st_bool then
		ErrorAtNode(COND, @eTYPE_COND_EXPR);
	end:if

	if str_ne(@Buff,":") then
		Stop(@eTHENEXP);
	end:if

	Scan(@Buff);

	Node[BODY].pLeft = Ctrl(@Buff);

	SkipFinalScan = 1;

	CTE_SELECT(P1);
end


/*****************************************************************************/

bool Statement_SWITCH_CheckCast(word pTargetType; word pLiteralType)
	select
	case pTargetType == st_char:
		when pLiteralType == st_char:
			return true;
	case pTargetType == st_byte:
		when pLiteralType == st_word:
			return true;
	case pTargetType == st_word:
		when pLiteralType == st_word:
			return true;
	case pTargetType == st_int:
		when pLiteralType == st_word | pLiteralType == st_int:
			return true;
	end:select
	return false;
end

void Statement_SWITCH(char @Buff; word @P1; word @SkipFinalScan)

	Node[P1].ID = iSWITCH;

	word COND = Peek();
	Node[COND].ID = iCOND;
	Node[P1].pLeft = COND;

	word @CaseBackLink = @Node[COND].pRight;

	Node[COND].pLeft = Expr(PRIO_ZERO, @Scan(@Buff));
	word pType = Node[Node[COND].pLeft].pType;

	when pType != st_char & pType != st_byte & pType != st_word & pType != st_int:
		Stop(@eSWTYPE);

	when str_ne(@Buff,"of"):
		Stop(@eOFEXP);

	when str_ne(@Scan(@Buff),"case"):
		Stop(@eCASEEXP);

	while str_eq(@Buff,"case") do
		word CASE = Peek();
		Node[CASE].ID = iCASE;
		CaseBackLink = CASE;
		@CaseBackLink = @Node[CASE].pRight;
		word @ValueBackLink = @Node[CASE].pLeft;

		repeat
			word EXPR = Expr(PRIO_ZERO, @Scan(@Buff));

			when !NodeHasImmediateValue(EXPR):
				Stop(@eCONSTEXP);

			when !Statement_SWITCH_CheckCast(pType, Node[EXPR].pType):
				Stop(@eCONSTEXP);

			word  CASE_I = Node[COND].pRight;
			while CASE_I < nNODE do
				word  VALUE_I = Node[CASE_I].pLeft;
				while VALUE_I < nNode & Node[VALUE_I].ID == iVALUE do
					if Node[Node[VALUE_I].pLeft].Value == Node[EXPR].Value then
						Stop(@eDUPCASE);
					end:if

					VALUE_I = Node[VALUE_I].pRight;
				end:while

				CASE_I = Node[CASE_I].pRight;
			end:while

			word VALUE = Peek();
			Node[VALUE].ID = iVALUE;
			Node[VALUE].pLeft = EXPR;
			ValueBackLink = VALUE;
			@ValueBackLink = @Node[VALUE].pRight;

		until str_ne(@Buff,",");

		if str_ne(@Buff,":") then
			Stop(@eCOLONEXP);
		end

		word BODY = Peek();
		Node[BODY].ID = iBODY;
		ValueBackLink = BODY;

		Scan(@Buff);
		Node[BODY].pLeft = Statements(@Buff, "case", "default", "end");
	end

	if str_eq(@Buff,"default") then
		if str_ne(@Scan(@Buff),":") then
			Stop(@eCOLONEXP);
		end

		word CASE = Peek();
		word DEFAULT = Peek();
		word BODY = Peek();

		Node[CASE].ID = iCASE;
		Node[DEFAULT].ID = iDEFAULT;
		Node[BODY].ID = iBODY;

		Node[CASE].pLeft = DEFAULT;
		Node[DEFAULT].pRight = BODY;
		CaseBackLink = CASE;

		Scan(@Buff);
		Node[BODY].pLeft = Statements(@Buff, "end", NULL, NULL);
	end:if

	SkipFinalScan = EndOf(@Buff, "switch", NULL, true);

	CTE_SWITCH(P1);
end

/*****************************************************************************/

void Statement_WHILE(char @Buff; word @P1; word @SkipFinalScan)

	word  BODY = Peek();
	word  COND = Peek();
	Node [P1].ID = iWHILE;
	Node [P1].pLeft = BODY;

	Node [BODY].ID = iBODY;
	Node [BODY].pRight = COND;

	Node [COND].ID = iCOND;

	char @End2 = NULL;

	select
	case str_eq(@Buff,"while"):
		@End2 = "while";
		Node[COND].pLeft = Expr(PRIO_ZERO, @Scan(@Buff));
		word pType = Node[Node[COND].pLeft].pType;

		if pType != st_bool then
			Error(@eTYPE);
		end

		if str_ne(@Buff,"do") then
			Stop(@eDOEXP);
		end
	case str_eq(@Buff,"forever"):
		@End2 = "forever";
		Scan(@Buff);
		if str_ne(@Buff,"do") then
			Stop(@eDOEXP);
		end
	case str_eq(@Buff,"do"):
		Deprecated("'do' should be replaced with 'forever do'");
	default:
		StopInternal(__FILE__, __LINE__);
	end:select

	word pLoop1=pLoop;
	pLoop=P1;

	Scan(@Buff);
	Node[BODY].pLeft = Statements(@Buff, "end", NULL, NULL);

	SkipFinalScan = EndOf(@Buff, "do", @End2, true);

	pLoop=pLoop1;

	CTE_WHILE(@P1);
end

/*****************************************************************************/

void Statement_REPEAT(char @Buff; word @P1; word @SkipFinalScan)
	word  BODY = Peek();
	word  COND = Peek();
	Node[P1].ID = iREPEAT;
	Node[P1].pLeft = BODY;

	Node[BODY].ID = iBODY;
	Node[BODY].pRight = COND;

	Node[COND].ID = iCOND;

	word pLoop1=pLoop;
	pLoop=P1;

	Scan(@Buff);
	Node[BODY].pLeft = Statements(@Buff, "until", NULL, NULL);

	Node[COND].pLeft = Expr(PRIO_ZERO, @Scan(@Buff));
	word pType = Node[Node[COND].pLeft].pType;

	if str_ne(@Buff,";") then
		Stop(@eSEMICOLONEXP);
	end

	if pType != st_bool then
		Stop(@eTYPE);
	end

	pLoop=pLoop1;

	CTE_REPEAT(@P1);
end

/*****************************************************************************/

void Statement_CONTINUE(char @Buff; word @P1; word @SkipFinalScan)
	when pLoop>=nNODE:
		Stop(@eWRONGCONTINUE);

	when Node[pLoop].ID != iWHILE
	&    Node[pLoop].ID != iREPEAT:
		StopInternal(__FILE__, __LINE__);

	Node[P1].ID = iCONTINUE;
	Node[P1].Value = pLoop;
	Node[P1].pType = st_noreturn;

	MatchSemicolon(@Buff);
end

/*****************************************************************************/

void Statement_EXIT(char @Buff; word @P1; word @SkipFinalScan)
	when pLoop>=nNODE:
		Stop(@eWRONGEXIT);

	when Node[pLoop].ID != iWHILE
	&    Node[pLoop].ID != iREPEAT:
		StopInternal(__FILE__, __LINE__);

	Node[P1].ID = iEXIT;
	Node[P1].Value = pLoop;
	Node[P1].pType = st_noreturn;

	MatchSemicolon(@Buff);
end

/*****************************************************************************/

void Statement_INCDEC(char @Buff; word @P1; word @SkipFinalScan)

	if str_eq(@Buff,"inc") then
		Node[P1].ID =iINC;
	else
		Node[P1].ID =iDEC;
	end:if

	Node[P1].pLeft= Expr(PRIO_INC, @Scan(@Buff));
	word pType = Node[Node[P1].pLeft].pType;

	if str_ne(@Buff,";") then
		Stop(@eSEMICOLONEXP);
	end

	select
	case !NodeIsLValue(Node[P1].pLeft):
		ErrorAtNode(P1, @eLVALUE_IN_INC);
		AddFakeCast(st_word, @Node[P1].pLeft);
	case pType!=st_byte & pType!=st_word & pType!=st_int:
		ErrorAtNode(P1, @eTYPE);
		AddFakeCast(st_word, @Node[P1].pLeft);
	end:select

end

/*****************************************************************************/

void Statement_RETURN(char @Buff; word @P1; word @SkipFinalScan)

	Node[P1].ID   =iRETURN;
	Scan(@Buff);

	if Dict[pCurrentFunc].pType == st_noreturn then
		Stop(@eRETINNORET);
	end:if

	if Dict[pCurrentFunc].pType != st_void then
		Node[P1].pLeft=Expr(PRIO_ZERO, @Buff);
		ImplicitCast(Dict[pCurrentFunc].pType, @Node[P1].pLeft);
	end:if

	if str_ne(@Buff,";") then
		Stop(@eSEMICOLONEXP);
	end

	Node[P1].pType = st_noreturn;
end

/*****************************************************************************/

void Statement_INLINE(char @Buff; word @P1; word @SkipFinalScan)

	word Code=val(@Scan(@Buff));
	if Code>255 then
		Stop(@eOVERFLOW);
	end

	Node[P1].ID   =iINLINE;
	Node[P1].Value= Code;

	if str_eq(@Scan(@Buff),",") then
		strcpy (@Buff,"inline");
		SkipFinalScan = 1;
		return;
	end

	if str_ne(@Buff,";") then
		Stop(@eSEMICOLONEXP);
	end

end

/*****************************************************************************/

void Statement_DEF_LOCAL(char @Buff; word @_P1; word @SkipFinalScan; word pType)
	if pType>=nDict | Dict[pType].Class!=cTYPE then
		StopInternal(__FILE__, __LINE__);
	end:if

	word P1 = _P1;

	while true do

		word nPtr=ReadPtrs(@Buff);
		if pType==st_void & nPtr<1 then
			Stop(@eNOVOID);
		end:if

		if Dict[pType].Sub==sFUNCTYPE & nPtr<1 then
			Stop(@eNOVAL);
		end:if

		word I = ResolveLocal(@Buff);
		when I < nLOCAL:
			StopWithSubject(@eDUPLICATE, @Buff);

		I = ResolveParm(@Buff);
		when I < nDICT:
			StopWithSubject(@eDUPLICATE, @Buff);

		if nLocal>=nLOCAL then
			StopMemory();
		end:if

		Local[nLocal].Name2=SaveString0(@Buff);
		Local[nLocal].pType=T_nPtrTo(nPtr, pType);
		Local[nLocal].pType=Tabl_(@Scan(@Buff), Local[nLocal].pType);
		//Local[nLocal].nIndx= Tabl(@Scan(@Buff));

		word  N=1;
		if T_IsArray(Local[nLocal].pType) then
			N = T_ArrayGetLen(Local[nLocal].pType);
		end

		word P4=Peek();

		Node[P1].ID   =iDEF_LOCAL;
		Node[P1].pType=Local[nLocal].pType;
		Node[P1].Value=Local[nLocal].Name2;
		Node[P1].pLeft =P4;

		Node[P4].ID    =iEMPTY;
		Node[P4].Value = N;

		if str_eq(@Buff,"=") then
			/*if Local[nLocal].nIndx > 0 then
				Stop(@eNOTAVAIL);
			end:if*/

			word P5=Peek();
			Node[P4].ID    =iASSIGN;
			Node[P4].pLeft = P5;

			word P6=Peek();
			Node[P5].ID    =iEMPTY;
			Node[P5].pLeft = P6;
			Node[P5].pRight= Expr(PRIO_ZERO, @Scan(@Buff));

			Node[P6].ID    =iLOCAL;
			Node[P6].Value =nLocal;
			Node[P6].pType =Local[nLocal].pType;

			ImplicitCast(T_nPtrTo(nPtr, pType), @Node[P5].pRight);
		end:if

		inc nLocal;

		if str_ne(@Buff,",") then
			return;
		end:if
		Scan(@Buff);

		word pNext = Peek();
		Node[P1].pRight = pNext;
		P1 = pNext;
	end:while
end

void Statement_ASSIGN(char @Buff; word @P1; word @SkipFinalScan)

	/*
		TODO: complete scheme to be implemented:
		expr = expr2
		expr [@] name [suffix] [ = expr2 ]
		expr [@] name [suffix] [ of codeblock ]
		expr name [ = expr2 ] {, name [ = expr2 ]}
	*/

	word pLValue = Expr(PRIO_LVALUE, @Buff);

	when pLValue == nNODE:
		StopInternal(__FILE__, __LINE__);

	if Node[pLValue].ID == iTYPE then
		Statement_DEF_LOCAL(@Buff, @P1, @SkipFinalScan, Node[pLValue].Value);
	else
		word P3=Peek();
		Node[P1].ID    = iASSIGN;
		Node[P1].pLeft = P3;

		Node[P3].ID    = iEMPTY;
		Node[P3].pLeft = pLValue;

		if str_eq(@Buff,"=") then
			if !NodeIsLValue(Node[P3].pLeft) then
				Error(@eLVALUE_IN_ASSIGNMENT);
			end:if
			Node[P3].pRight = Expr(PRIO_ZERO, @Scan(@Buff));
			ImplicitCast(Node[pLValue].pType, @Node[P3].pRight);
		end:if

		if Node[pLValue].pType == st_noreturn then
			Node[P1].pType = st_noreturn;
		else
			Node[P1].pType = st_void;
		end:if
	end:if

	if str_ne(@Buff,";") then
		Stop(@eSEMICOLONEXP);
	end:if
end

/*****************************************************************************/

word Ctrl(char @Buff)

	word SkipFinalScan = 0;

	word P1 = Peek();

	select
		case str_eq(@Buff,"if") | str_eq(@Buff,"select"):
			Statement_IF(@Buff, @P1, @SkipFinalScan);

		case str_eq(@Buff,"while") | str_eq(@Buff,"do") | str_eq(@Buff,"forever"):
			Statement_WHILE(@Buff, @P1, @SkipFinalScan);

		case str_eq(@Buff,"when"):
			Statement_WHEN(@Buff, @P1, @SkipFinalScan);

		case str_eq(@Buff,"switch"):
			Statement_SWITCH(@Buff, @P1, @SkipFinalScan);

		case str_eq(@Buff,"repeat"):
			Statement_REPEAT(@Buff, @P1, @SkipFinalScan);

		case str_eq(@Buff,"continue"):
			Statement_CONTINUE(@Buff, @P1, @SkipFinalScan);

		case str_eq(@Buff,"exit"):
			Statement_EXIT(@Buff, @P1, @SkipFinalScan);

		case str_eq(@Buff,"inc") | str_eq(@Buff,"dec"):
			Statement_INCDEC(@Buff, @P1, @SkipFinalScan);

		case str_eq(@Buff,"inline"):
			Statement_INLINE(@Buff, @P1, @SkipFinalScan);

		case str_eq(@Buff,"return"):
			Statement_RETURN(@Buff, @P1, @SkipFinalScan);

		case str_eq(@Buff,"pass"):
			Node[P1].ID   =iEMPTY;
			MatchSemicolon(@Buff);

		case str_eq(@Buff,"null"):
			Node[P1].ID   =iEMPTY;
			MatchSemicolon(@Buff);
			Warn(@dprNULL);

		default:
			Statement_ASSIGN(@Buff, @P1, @SkipFinalScan);
	end:select

	if SkipFinalScan == 0 then
		Scan (@Buff);
	end

	return P1;
end

/*****************************************************************************/

word Statements(char @Buff; char @End1; char @End2; char @End3)

	word Saved_nLocal = nLocal;

	word P = nNODE;
	word @Backlink = @P;

	repeat
		word pCtrl = Ctrl(@Buff);
		Backlink = pCtrl;
		while Node[pCtrl].pRight != nNODE do
			pCtrl = Node[pCtrl].pRight;
		end
		@Backlink =@Node[pCtrl].pRight;

		if @End2 != NULL & str_eq(@Buff, @End2) then
			exit;
		end
		if @End3 != NULL & str_eq(@Buff, @End3) then
			exit;
		end

	until str_eq(@Buff, @End1);

	nLocal = Saved_nLocal;

	CTE_CodeList(P);

	return P;
end

/*****************************************************************************/

