
namespace StatementParser of

/*****************************************************************************/

void MakePassStatement(word pCtrl) of
	Node[pCtrl].ID = NID.iEMPTY;
	Node[pCtrl].pType = st_void;
end

/*****************************************************************************/

void MatchSemicolon(char @Buff; bool @SkipFinalScan)
	if str_ne(@Buff,";") then
		Error(@L10n.eSEMICOLONEXP);
		SkipFinalScan = true;
	else
		SkipFinalScan = false;
	end
end

void ScanAndMatchSemicolon(char @Buff; bool @SkipFinalScan)
	Scan(@Buff);
	MatchSemicolon(@Buff, @SkipFinalScan);
end

/*****************************************************************************/

void ProcessSeparatorKeywordError(char @Buff; char @Keyword)
	bool EatIt =
		str_eq(@Buff, "then") |
		str_eq(@Buff, ":") |
		str_eq(@Buff, "loop") |
		str_eq(@Buff, "do") |
		str_eq(@Buff, "of")
	;

	char MsgBuff[nBUFF];
	strbuf buf;
	strbuf.init(@buf, @MsgBuff, countof(MsgBuff));

	strbuf.append_cstr3(@buf,
		@L10n.eMISSING_SEP_1,
		@Keyword,
		@L10n.eMISSING_SEP_2
	);
	Error(@buf.Buff);

	if EatIt then
		strbuf.truncate(@buf, 0);
		strbuf.append_cstr3(@buf,
			@L10n.eWRONG_SEP_1,
			@Buff,
			@L10n.eWRONG_SEP_2
		);
		Error(@buf.Buff);
		Scan(@Buff);
	end:if
end

void EatSeparatorKeyword(char @Buff; char @Keyword)
	if str_eq(@Buff, @Keyword) then
		Scan(@Buff);
		return;
	end:if
	ProcessSeparatorKeywordError(@Buff, @Keyword);
end

char @EatSeparatorKeyword2(char @Buff; char @Keyword; char @Keyword2)
	if str_eq(@Buff, @Keyword) then
		Scan(@Buff);
		return @Keyword;
	end:if
	if str_eq(@Buff, @Keyword2) then
		Scan(@Buff);
		return @Keyword2;
	end:if
	ProcessSeparatorKeywordError(@Buff, @Keyword);
	return @Keyword;
end

/*****************************************************************************/

bool EndOf(char @Buff, @A1, @A2; bool EatSemicolon)
	when str_ne(@Buff, "end"):
		StopInternal(__FILE__, __LINE__);

	Scan(@Buff);
	if str_eq(@Buff, ":") then
		Scan(@Buff);
		if str_eq(@Buff, @A1)
		| (@A2 != NULL & str_eq(@Buff, @A2)) then
			pass;
		else
			char MsgBuff[nBUFF];
			strbuf buf;
			strbuf.init(@buf, @MsgBuff, countof(MsgBuff));
			strbuf.append_cstr5(@buf,
				@L10n.eWRONG_END_ID_1,
				@Buff,
				@L10n.eWRONG_END_ID_2,
				@A1,
				@L10n.eWRONG_END_ID_3
			);
			if @A2 != NULL then
				strbuf.append_cstr3(@buf,
					@L10n.eWRONG_END_ID_4,
					@A2,
					@L10n.eWRONG_END_ID_5
				);
			end:if
			Error(@buf.Buff);
		end:if
		Scan(@Buff);
	end:if

	if EatSemicolon & str_eq(@Buff, ";") then
		return false;
	else
		return true;
	end
end

/*****************************************************************************/

void Statement_IF(char @Buff; word @P1; bool @SkipFinalScan)
	Node[P1].ID = NID.iSELECT;

	char @End1="if";
	char @Case="";
	char @Then="then";
	char @Else="else";
	if str_eq(@Buff,"select") then
		@End1="select";
		@Case="case";
		@Then=":";
		@Else="default";

		if str_ne(@Scan(@Buff),"case") then
			Stop(@L10n.eCASEEXP);
		end:if
	else
		strcpy(@Buff,@Case);
	end:if

	word @BackLink = @Node[P1].pLeft;
	while str_ne(@Buff,"end") do
		word CASE = Peek();
		word COND = Peek();
		word BODY = Peek();

		Node[CASE].ID = NID.iCASE;
		Node[COND].ID = NID.iCOND;
		Node[BODY].ID = NID.iBODY;

		Node[CASE].pLeft  = COND;
		Node[COND].pRight = BODY;
		BackLink = CASE;
		@BackLink = @Node[CASE].pRight;

		select
		case str_eq(@Buff,@Case):
			Node[COND].pLeft = Expr(PRIO.ZERO, @Scan(@Buff));
			word pType = Node[Node[COND].pLeft].pType;

			if pType != st_bool then
				ErrorAtNode(COND, @L10n.eTYPE_COND_EXPR);
			end:if

			EatSeparatorKeyword(@Buff, @Then);
			Node[BODY].pLeft = Statements(@Buff, @Case, @Else, "end");
		case str_eq(@Buff, @Else):
			if str_eq(@Else,"default") then
				Scan(@Buff);
				EatSeparatorKeyword(@Buff, ":");
			else
				Scan(@Buff);
			end:if
			@Case="";
			@Else="";
			Node[BODY].pLeft = Statements(@Buff, @Case, @Else, "end");
		end:select
	end:while

	SkipFinalScan = EndOf(@Buff, @End1, NULL, true);

	CTE_SELECT(P1);
end

/*****************************************************************************/

void Statement_WHEN(char @Buff; word @P1; bool @SkipFinalScan)
	Node[P1].ID = NID.iSELECT;

	word CASE = Peek();
	word COND = Peek();
	word BODY = Peek();

	Node[CASE].ID = NID.iCASE;
	Node[COND].ID = NID.iCOND;
	Node[BODY].ID = NID.iBODY;

	Node[CASE].pLeft  = COND;
	Node[COND].pRight = BODY;
	Node[P1].pLeft = CASE;

	Node[COND].pLeft = Expr(PRIO.ZERO, @Scan(@Buff));
	word pType = Node[Node[COND].pLeft].pType;

	if pType != st_bool then
		ErrorAtNode(COND, @L10n.eTYPE_COND_EXPR);
	end:if

	EatSeparatorKeyword(@Buff, ":");

	Node[BODY].pLeft = Ctrl(@Buff);

	SkipFinalScan = true;

	CTE_SELECT(P1);
end

/*****************************************************************************/

bool Statement_SWITCH_CheckCast(word pTargetType; word pLiteralType)
	select
	case pTargetType == st_char:
		when pLiteralType == st_char:
			return true;
	case pTargetType == st_byte:
		when pLiteralType == st_word:
			return true;
	case pTargetType == st_word:
		when pLiteralType == st_word:
			return true;
	case pTargetType == st_int:
		when pLiteralType == st_word | pLiteralType == st_int:
			return true;
	end:select
	return false;
end

void Statement_SWITCH(char @Buff; word @P1; bool @SkipFinalScan)

	Node[P1].ID = NID.iSWITCH;

	word COND = Peek();
	Node[COND].ID = NID.iCOND;
	Node[P1].pLeft = COND;

	word @CaseBackLink = @Node[COND].pRight;

	Node[COND].pLeft = Expr(PRIO.ZERO, @Scan(@Buff));
	word pType = Node[Node[COND].pLeft].pType;

	when pType != st_char & pType != st_byte & pType != st_word & pType != st_int:
		Stop(@L10n.eSWTYPE);

	EatSeparatorKeyword(@Buff, "of");

	when str_ne(@Buff,"case"):
		Stop(@L10n.eCASEEXP);

	while str_eq(@Buff,"case") do
		word CASE = Peek();
		Node[CASE].ID = NID.iCASE;
		CaseBackLink = CASE;
		@CaseBackLink = @Node[CASE].pRight;
		word @ValueBackLink = @Node[CASE].pLeft;

		repeat
			word EXPR = Expr(PRIO.ZERO, @Scan(@Buff));

			when !NodeHasImmediateValue(EXPR):
				Error(@L10n.eCONSTEXP);

			when !Statement_SWITCH_CheckCast(pType, Node[EXPR].pType):
				Error(@L10n.eCONSTEXP);

			word  CASE_I = Node[COND].pRight;
			while CASE_I < nNODE do
				word  VALUE_I = Node[CASE_I].pLeft;
				while VALUE_I < nNode & Node[VALUE_I].ID == NID.iVALUE do
					if Node[Node[VALUE_I].pLeft].Value == Node[EXPR].Value then
						Error(@L10n.eDUPCASE);
					end:if

					VALUE_I = Node[VALUE_I].pRight;
				end:while

				CASE_I = Node[CASE_I].pRight;
			end:while

			word VALUE = Peek();
			Node[VALUE].ID = NID.iVALUE;
			Node[VALUE].pLeft = EXPR;
			ValueBackLink = VALUE;
			@ValueBackLink = @Node[VALUE].pRight;

		until str_ne(@Buff,",");

		word BODY = Peek();
		Node[BODY].ID = NID.iBODY;
		ValueBackLink = BODY;

		EatSeparatorKeyword(@Buff, ":");

		Node[BODY].pLeft = Statements(@Buff, "case", "default", "end");
	end

	if str_eq(@Buff,"default") then
		Scan(@Buff);

		word CASE = Peek();
		word DEFAULT = Peek();
		word BODY = Peek();

		Node[CASE].ID = NID.iCASE;
		Node[DEFAULT].ID = NID.iDEFAULT;
		Node[BODY].ID = NID.iBODY;

		Node[CASE].pLeft = DEFAULT;
		Node[DEFAULT].pRight = BODY;
		CaseBackLink = CASE;

		EatSeparatorKeyword(@Buff, ":");
		Node[BODY].pLeft = Statements(@Buff, "end", NULL, NULL);
	end:if

	SkipFinalScan = EndOf(@Buff, "switch", NULL, true);

	CTE_SWITCH(P1);
end

/*****************************************************************************/

void Statement_WHILE(char @Buff; word @P1; bool @SkipFinalScan)

	word  BODY = Peek();
	word  COND = Peek();
	Node [P1].ID = NID.iWHILE;
	Node [P1].pLeft = BODY;

	Node [BODY].ID = NID.iBODY;
	Node [BODY].pRight = COND;

	Node [COND].ID = NID.iCOND;

	char @End1 = NULL;
	char @End2 = NULL;

	select
	case str_eq(@Buff,"while"):
		@End2 = "while";
		Node[COND].pLeft = Expr(PRIO.ZERO, @Scan(@Buff));
		word pType = Node[Node[COND].pLeft].pType;
		if pType != st_bool then
			Error(@L10n.eTYPE);
		end
		@End1 = @EatSeparatorKeyword2(@Buff, "loop", "do");
	case str_eq(@Buff,"forever"):
		@End2 = "forever";
		Scan(@Buff);
		@End1 = @EatSeparatorKeyword2(@Buff, "loop", "do");
	default:
		StopInternal(__FILE__, __LINE__);
	end:select

	word pLoop1=pLoop;
	pLoop=P1;

	Node[BODY].pLeft = Statements(@Buff, "end", NULL, NULL);

	SkipFinalScan = EndOf(@Buff, @End1, @End2, true);

	pLoop=pLoop1;

	CTE_WHILE(@P1);
end

/*****************************************************************************/

void Statement_REPEAT(char @Buff; word @P1; bool @SkipFinalScan)
	word  BODY = Peek();
	word  COND = Peek();
	Node[P1].ID = NID.iREPEAT;
	Node[P1].pLeft = BODY;

	Node[BODY].ID = NID.iBODY;
	Node[BODY].pRight = COND;

	Node[COND].ID = NID.iCOND;

	word pLoop1=pLoop;
	pLoop=P1;

	Scan(@Buff);
	Node[BODY].pLeft = Statements(@Buff, "until", NULL, NULL);

	Node[COND].pLeft = Expr(PRIO.ZERO, @Scan(@Buff));
	word pType = Node[Node[COND].pLeft].pType;

	MatchSemicolon(@Buff, @SkipFinalScan);

	if pType != st_bool then
		Stop(@L10n.eTYPE);
	end

	pLoop=pLoop1;

	CTE_REPEAT(@P1);
end

/*****************************************************************************/

define nWRONGLABEL (nNODE + 1)

word ParseOptionalLabel(char @Buff; bool @SkipFinalScan) of
	word pLabelTarget = nNODE;

	select
	case str_eq(@Buff, ";"):
		return pLabelTarget;
	case IsKeyword(@Buff):
		Error(@L10n.eSEMICOLONEXP);
		SkipFinalScan = true;
		return pLabelTarget;
	end:select

	word iLabel = ResolveLocal(@Buff);
	select
	case iLabel == nLOCAL:
		ErrorWithSubject(@L10n.eNOLABEL, @Buff);
		pLabelTarget = nWRONGLABEL;
	case Local[iLabel].pType == st_outofscope_label:
		ErrorWithSubject(@L10n.eLABELOUTOFSCOPE, @Buff);
		pLabelTarget = nWRONGLABEL;
	case Local[iLabel].pType != st_label:
		ErrorWithSubject(@L10n.eNOTALABEL, @Buff);
		pLabelTarget = nWRONGLABEL;
	default:
		word pLabel = Local[iLabel].Value;
		when Node[pLabel].ID != NID.iDEF_LABEL:
			StopInternal(__FILE__, __LINE__);
		word pLabelLink = Node[pLabel].pLeft;
		when pLabelLink == nNODE:
			StopInternal(__FILE__, __LINE__);
		pLabelTarget = Node[pLabelLink].Value;
	end:select

	ScanAndMatchSemicolon(@Buff, @SkipFinalScan);
	return pLabelTarget;
end

void Statement_CONTINUE_EXIT(char @Buff; word @P1; bool @SkipFinalScan; word nID) of
	word pLabelTarget = ParseOptionalLabel(@Scan(@Buff), @SkipFinalScan);

	char @eWRONG_STATEMENT;
	switch nID of
	case NID.iCONTINUE: @eWRONG_STATEMENT = @L10n.eWRONGCONTINUE;
	case NID.iEXIT:     @eWRONG_STATEMENT = @L10n.eWRONGEXIT;
	default:            StopInternal(__FILE__, __LINE__);
	end:switch

	select
	case pLabelTarget == nWRONGLABEL:
		/* Diagnostic message already printed by ParseOptionalLabel(),
		just return. */
		MakePassStatement(P1);
		return;
	case pLabelTarget == nNODE:
		if pLoop == nNODE then
			Error(@eWRONG_STATEMENT);
			MakePassStatement(P1);
			return;
		end:if

		when Node[pLoop].ID != NID.iWHILE
		&    Node[pLoop].ID != NID.iREPEAT:
			StopInternal(__FILE__, __LINE__);
		pLabelTarget = pLoop;
	default:
		if Node[pLabelTarget].ID != NID.iWHILE
		&  Node[pLabelTarget].ID != NID.iREPEAT then
			Error(@eWRONG_STATEMENT);
			MakePassStatement(P1);
			return;
		end:if
	end:select

	Node[P1].ID = nID;
	Node[P1].Value = pLabelTarget;
	Node[P1].pType = st_noreturn;
end

/*****************************************************************************/

void Statement_CONTINUE(char @Buff; word @P1; bool @SkipFinalScan)
	Statement_CONTINUE_EXIT(@Buff, @P1, @SkipFinalScan, NID.iCONTINUE);
end

/*****************************************************************************/

void Statement_EXIT(char @Buff; word @P1; bool @SkipFinalScan)
	Statement_CONTINUE_EXIT(@Buff, @P1, @SkipFinalScan, NID.iEXIT);
end

/*****************************************************************************/

void Statement_INCDEC(char @Buff; word @P1; bool @SkipFinalScan)

	if str_eq(@Buff,"inc") then
		Node[P1].ID = NID.iINC;
	else
		Node[P1].ID = NID.iDEC;
	end:if

	Node[P1].pLeft= Expr(PRIO.INC, @Scan(@Buff));
	word pType = Node[Node[P1].pLeft].pType;

	MatchSemicolon(@Buff, @SkipFinalScan);

	select
	case !NodeIsLValue(Node[P1].pLeft):
		ErrorAtNode(P1, @L10n.eLVALUE_IN_INC);
		AddFakeCast(st_word, @Node[P1].pLeft);
	case pType!=st_byte & pType!=st_word & pType!=st_int:
		ErrorAtNode(P1, @L10n.eTYPE);
		AddFakeCast(st_word, @Node[P1].pLeft);
	end:select

end

/*****************************************************************************/

void Statement_RETURN(char @Buff; word @P1; bool @SkipFinalScan)

	Node[P1].ID = NID.iRETURN;
	Scan(@Buff);

	word pTypeReturn = Func_GetRetType(pCurrentFunc);

	if pTypeReturn == st_noreturn then
		Stop(@L10n.eRETINNORET);
	end:if

	if pTypeReturn != st_void then
		Node[P1].pLeft = Expr(PRIO.ZERO, @Buff);
		ImplicitCast(pTypeReturn, @Node[P1].pLeft);
	end:if

	MatchSemicolon(@Buff, @SkipFinalScan);

	Node[P1].pType = st_noreturn;
end

/*****************************************************************************/

void Statement_INLINE(char @Buff; word @P1; bool @SkipFinalScan)

	word Code = val(@Scan(@Buff));
	if Code > 255 then
		Stop(@L10n.eOVERFLOW);
	end

	Node[P1].ID    = NID.iINLINE;
	Node[P1].Value = Code;

	if str_eq(@Scan(@Buff),",") then
		strcpy (@Buff,"inline");
		SkipFinalScan = true;
		return;
	end

	MatchSemicolon(@Buff, @SkipFinalScan);
end

/*****************************************************************************/

void Statement_DEF_LOCAL(char @Buff; word @_P1; bool @SkipFinalScan; word pPrefixType)

	word P1 = _P1;

	while true do
		word nPtr=ReadPtrs(@Buff);

		word I = ResolveLocal(@Buff);
		when I < nLOCAL:
			ErrorWithSubject(@L10n.eDUPLICATE, @Buff);

		I = ResolveParm(@Buff);
		when I < nDICT:
			ErrorWithSubject(@L10n.eDUPLICATE, @Buff);

		EnsureValidIdentifier(@Buff);

		word Name2 = SaveString0(@Buff);
		word pType = ReadTypeSuffix(@Scan(@Buff), T_nPtrTo(nPtr, pPrefixType));

		if pType == st_void then
			Stop(@L10n.eNOVOID);
		end:if

		if T_IsFunc(pType) then
			Stop(@L10n.eNOVAL);
		end:if

		word iLocal = AllocLocal();
		Local[iLocal].Name2 = Name2;
		Local[iLocal].pType = pType;

		word N = 1;
		if T_IsArray(Local[iLocal].pType) then
			N = T_ArrayGetLen(Local[iLocal].pType);
		end

		word P4=Peek();

		Node[P1].ID    = NID.iDEF_LOCAL;
		Node[P1].pType = Local[iLocal].pType;
		Node[P1].Value = Local[iLocal].Name2;
		Node[P1].pLeft = P4;

		Node[P4].ID    = NID.iEMPTY;
		Node[P4].Value = N;

		if str_eq(@Buff,"=") then
			/*if Local[iLocal].nIndx > 0 then
				Stop(@L10n.eNOTAVAIL);
			end:if*/

			word P5=Peek();
			Node[P4].ID     = NID.iASSIGN;
			Node[P4].pLeft  = P5;

			word P6=Peek();
			Node[P5].ID     = NID.iEMPTY;
			Node[P5].pLeft  = P6;
			Node[P5].pRight = Expr(PRIO.ZERO, @Scan(@Buff));

			Node[P6].ID     = NID.iLOCAL;
			Node[P6].Value  = iLocal;
			Node[P6].pType  = Local[iLocal].pType;

			ImplicitCast(pType, @Node[P5].pRight);
		end:if

		if str_ne(@Buff,",") then
			return;
		end:if
		Scan(@Buff);

		word pNext = Peek();
		Node[P1].pRight = pNext;
		P1 = pNext;
	end:while
end

void Statement_ASSIGN(char @Buff; word @P1; bool @SkipFinalScan)

	word pLValue = Expr(PRIO.LVALUE, @Buff);

	when pLValue == nNODE:
		StopInternal(__FILE__, __LINE__);

	if Node[pLValue].ID == NID.iTYPE then
		Statement_DEF_LOCAL(@Buff, @P1, @SkipFinalScan, Node[pLValue].Value);
	else
		word P3=Peek();
		Node[P1].ID    = NID.iASSIGN;
		Node[P1].pLeft = P3;

		Node[P3].ID    = NID.iEMPTY;
		Node[P3].pLeft = pLValue;

		if str_eq(@Buff,"=") then
			if !NodeIsLValue(Node[P3].pLeft) then
				Error(@L10n.eLVALUE_IN_ASSIGNMENT);
			end:if
			Node[P3].pRight = Expr(PRIO.ZERO, @Scan(@Buff));
			ImplicitCast(Node[pLValue].pType, @Node[P3].pRight);
		end:if

		if Node[pLValue].pType == st_noreturn then
			Node[P1].pType = st_noreturn;
		else
			Node[P1].pType = st_void;
		end:if
	end:if

	MatchSemicolon(@Buff, @SkipFinalScan);
end

/*****************************************************************************/

word ParseLabel(char @Buff) of
	when likely (Buff[0] != 'l' | !str_eq(@Buff, "label")):
		return nNODE;

	Scan(@Buff);

	word I = ResolveLocal(@Buff);
	when I < nLOCAL:
		ErrorWithSubject(@L10n.eDUPLICATE, @Buff);

	I = ResolveParm(@Buff);
	when I < nDICT:
		ErrorWithSubject(@L10n.eDUPLICATE, @Buff);

	EnsureValidIdentifier(@Buff);

	word Name2 = SaveString0(@Buff);
	word pType = st_label;

	word pLabel = Peek2(NID.iDEF_LABEL);
	word iLabel = AllocLocal();

	Local[iLabel].Name2 = Name2;
	Local[iLabel].pType = pType;
	Local[iLabel].Value = pLabel;

	Node[pLabel].Value = Name2;
	Node[pLabel].pType = pType;
	Node[pLabel].pLeft = Peek2(NID.iEMPTY);

	EatSeparatorKeyword(@Scan(@Buff), ":");

	return pLabel;
end

/*****************************************************************************/

byte CtrlPrefix0[256];
byte CtrlPrefix1[256];

void RegisterCtrlPrefix(char @Str)
	CtrlPrefix0[word(Str[0])] = 1;
	CtrlPrefix1[word(Str[1])] = 1;
end

void InitCtrlPrefix()
	RegisterCtrlPrefix("if");
	RegisterCtrlPrefix("select");
	RegisterCtrlPrefix("while");
	RegisterCtrlPrefix("do");
	RegisterCtrlPrefix("forever");
	RegisterCtrlPrefix("when");
	RegisterCtrlPrefix("switch");
	RegisterCtrlPrefix("repeat");
	RegisterCtrlPrefix("return");
	RegisterCtrlPrefix("continue");
	RegisterCtrlPrefix("exit");
	RegisterCtrlPrefix("inc");
	RegisterCtrlPrefix("dec");
	RegisterCtrlPrefix("inline");
	RegisterCtrlPrefix("pass");
	RegisterCtrlPrefix("null");
	RegisterCtrlPrefix(";");
end

/*****************************************************************************/

word Ctrl(char @Buff)

	bool SkipFinalScan = false;

	word pLabel = ParseLabel(@Buff);

	word pCtrl = Peek();
	Node[pCtrl].AttributeFlags = ReadAttributes(@Buff);

	if unlikely(pLabel != nNODE) then
		word pLabelLink = Node[pLabel].pLeft;
		when pLabelLink == nNODE:
			StopInternal(__FILE__, __LINE__);
		Node[pLabelLink].Value = pCtrl;
		Node[pLabel].pRight = pCtrl;
	end:if

	select
		case CtrlPrefix0[word(Buff[0])] == 0 | CtrlPrefix1[word(Buff[1])] == 0:
			Statement_ASSIGN(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"if") | str_eq(@Buff,"select"):
			Statement_IF(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"while") | str_eq(@Buff,"forever"):
			Statement_WHILE(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"when"):
			Statement_WHEN(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"switch"):
			Statement_SWITCH(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"repeat"):
			Statement_REPEAT(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"return"):
			Statement_RETURN(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"continue"):
			Statement_CONTINUE(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"exit"):
			Statement_EXIT(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"inc") | str_eq(@Buff,"dec"):
			Statement_INCDEC(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"inline"):
			Statement_INLINE(@Buff, @pCtrl, @SkipFinalScan);

		case str_eq(@Buff,"pass"):
			MakePassStatement(pCtrl);
			ScanAndMatchSemicolon(@Buff, @SkipFinalScan);

		case str_eq(@Buff,"null"):
			MakePassStatement(pCtrl);
			ScanAndMatchSemicolon(@Buff, @SkipFinalScan);
			Warn(@L10n.dprNULL);

		case str_eq(@Buff,";"):
			MatchSemicolon(@Buff, @SkipFinalScan);
			Error(@L10n.eSEMICOLONEXC);

		default:
			Statement_ASSIGN(@Buff, @pCtrl, @SkipFinalScan);
	end:select

	if !SkipFinalScan then
		Scan(@Buff);
	end

	if pLabel == nNODE then
		return pCtrl;
	else
		when Node[pLabel].pRight != pCtrl:
			StopInternal(__FILE__, __LINE__);

		/* Mark the label out of scope */
		char @LabelName = @Char[Node[pLabel].Value];
		word iLabel = ResolveLocal(@LabelName);
		when iLabel == nLOCAL:
			StopInternal(__FILE__, __LINE__);
		when Local[iLabel].pType != st_label:
			StopInternal(__FILE__, __LINE__);
		Local[iLabel].pType = st_outofscope_label;

		return pLabel;
	end:if
end

/*****************************************************************************/

word Statements(char @Buff; char @End1; char @End2; char @End3)

	word Saved_nLocal = nLocal;

	word P = nNODE;
	word @Backlink = @P;

	repeat
		word pCtrl = Ctrl(@Buff);
		Backlink = pCtrl;
		while Node[pCtrl].pRight != nNODE do
			pCtrl = Node[pCtrl].pRight;
		end
		@Backlink =@Node[pCtrl].pRight;

		if @End2 != NULL & str_eq(@Buff, @End2) then
			exit;
		end
		if @End3 != NULL & str_eq(@Buff, @End3) then
			exit;
		end

	until str_eq(@Buff, @End1);

	nLocal = Saved_nLocal;

	CTE_CodeList(P);

	return P;
end

/*****************************************************************************/

void InitStatementParser()
	InitCtrlPrefix();
end

/*****************************************************************************/

end:StatementParser;

alias ScanAndMatchSemicolon = StatementParser.ScanAndMatchSemicolon;
//alias MatchSemicolon = StatementParser.MatchSemicolon;
alias EndOf = StatementParser.EndOf;
alias Statements = StatementParser.Statements;
alias Ctrl = StatementParser.Ctrl;
alias InitStatementParser = StatementParser.InitStatementParser;
