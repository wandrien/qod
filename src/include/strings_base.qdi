$comment DOCUMENTATION ********************************************************

word strlen(char @St);
Возвращает количество символов строки, не включая завершающий 0-символ.

word strcmp(char @S1, @S2);
Возвращает 0 если строки равны и не 0 в противном случае.

char @strcpy(char @Dst, @Src);
Копирует строку @Src в @Dst. Возвращает значение @Dst.

char @str_push(char @Dst, @Src);
Копирует строку @Src в @Dst. Возвращает указатель на нулевой символ в строке @Dst.

char @strcat(char @Dst, @Src);
Производит конкатенацию строк @Dst и @Src и помещает результат в @Dst.

char @strn_cpy(char @Dst, @Src; word DstSize);
Копирует строку @Src в @Dst, при этом размер @Dst составляет DstSize байт.
Если строка @Src не умещается в строке @Dst, вызывает assert exception.
Возвращает значение @Dst.

char @strn_cat(char @Dst, @Src; word DstSize);
Производит конкатенацию строк @Dst и @Src и помещает результат в @Dst,
при этом размер @Dst составляет DstSize байт.
Если результат не умещается в строке @Dst, вызывает assert exception.
Возвращает значение @Dst.

***************************************************************** DOCUMENTATION

attribute(hot)
word strlen(char @St)
	word i = 0;
	while St[i] != #0 do
		inc i;
	end

	return i;
end

/*****************************************************************************/

attribute(hot)
word strcmp(char @S1, @S2)
	when @S1 == @S2:
		return 0;

	word i = 0;
	while S1[i] == S2[i] do
		if S1[i] == #0 then
			return 0;
		end

		inc i;
	end

	return 1;
end

/*****************************************************************************/

attribute(hot)
char @strcpy(char @Dst, @Src)
	word i = 0;
	while Src[i] != #0 do
		Dst[i] = Src[i];
		inc i;
	end
	Dst[i] = #0;

	return @Dst;
end

/*****************************************************************************/

attribute(hot)
char @str_push(char @Dst, @Src)
	word i = 0;
	while Src[i] != #0 do
		Dst[i] = Src[i];
		inc i;
	end
	Dst[i] = #0;

	return @Dst[i];
end

/*****************************************************************************/

char @strcat(char @Dst, @Src)
	strcpy(@Dst[strlen(@Dst)],@Src);
	return @Dst;
end

/*****************************************************************************/

attribute(hot)
char @strn_cpy(char @Dst, @Src; word DstSize)
	word i = 0;
	while Src[i] != #0 do
		if i >= DstSize then
			pass; /* FIXME: assert */
		end:if
		Dst[i] = Src[i];
		inc i;
	end
	
	if i >= DstSize then
		pass; /* FIXME: assert */
	end:if

	Dst[i] = #0;

	return @Dst;
end

/*****************************************************************************/

attribute(hot)
char @strn_cat(char @Dst, @Src; word DstSize)
	word DstLen = strlen(@Dst);
	if DstLen >= DstSize then
		pass; /* FIXME: assert */
	end:if
	strn_cpy(@Dst[strlen(@Dst)], @Src, DstSize - DstLen);
	return @Dst;
end

/*****************************************************************************/
