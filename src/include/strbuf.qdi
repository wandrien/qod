
struct strbuf of
	char @Buff;
	word BuffSize;
	word BuffLen;

	void init(strbuf @Dst; char @Buff; word BuffSize);
	char @deref_buff(strbuf @Dst; word Index);
	void truncate(strbuf @Dst; word NewLen);
	word get_size(strbuf @Dst);
	word get_virtual_len(strbuf @Dst);
	word get_actual_len(strbuf @Dst);
	word append_char(strbuf @Dst; char Char);
	word append_buf(strbuf @Dst; char @Src; word SrcLen);
	word append_cstr(strbuf @Dst; char @Src);
	word append_cstr2(strbuf @Dst; char @Src1; char @Src2);
	word append_cstr3(strbuf @Dst; char @Src1; char @Src2; char @Src3);
	word append_cstr4(strbuf @Dst; char @Src1; char @Src2; char @Src3;
	                               char @Src4);
	word append_cstr5(strbuf @Dst; char @Src1; char @Src2; char @Src3;
	                               char @Src4; char @Src5);
	word append_cstr6(strbuf @Dst; char @Src1; char @Src2; char @Src3;
	                               char @Src4; char @Src5; char @Src6);
end

void strbuf.init(strbuf @Dst; char @Buff; word BuffSize)
	when @Buff == NULL:
		BuffSize = 0;
	@Dst.Buff = @Buff;
	Dst.BuffSize = BuffSize;
	Dst.BuffLen = 0;
	when BuffSize > 0:
		Buff[0] = #0;
end

char @strbuf.deref_buff(strbuf @Dst; word Index)
	when @Dst.Buff == NULL:
		return NULL;
	when Index >= Dst.BuffSize:
		return NULL;
	return @Dst.Buff[Index];
end

void strbuf.truncate(strbuf @Dst; word NewLen)
	if NewLen < Dst.BuffLen then
		Dst.BuffLen = NewLen;
		if (@Dst.Buff != NULL) & (NewLen < Dst.BuffSize) then
			Dst.Buff[NewLen] = #0;
		end:if
	end:if
end

word strbuf.get_size(strbuf @Dst)
	return Dst.BuffSize;
end

word strbuf.get_virtual_len(strbuf @Dst)
	return Dst.BuffLen;
end

word strbuf.get_actual_len(strbuf @Dst)
	word BuffSize = Dst.BuffSize;
	if BuffSize == 0 then
		return 0;
	end
	word MaxRoom = BuffSize - 1;
	if Dst.BuffLen > MaxRoom then
		return MaxRoom;
	else
		return Dst.BuffLen;
	end
end

word strbuf.append_char(strbuf @Dst; char Char)
	char s[2];
	s[0] = Char;
	s[1] = #0;
	return strbuf.append_buf(@Dst, @s, 1);
end

word strbuf.append_buf(strbuf @Dst; char @Src; word SrcLen)
	when @Src == NULL:
		return Dst.BuffLen;
	word BuffSize = Dst.BuffSize;
	word OldLen = Dst.BuffLen;
	word NewLen = OldLen + SrcLen;
	if OldLen + 1 >= BuffSize then
		Dst.BuffLen = NewLen;
		return NewLen;
	end

	word RoomSize;
	if NewLen + 1 >= BuffSize then
		RoomSize = BuffSize - OldLen - 1;
	else
		RoomSize = SrcLen;
	end
	memcpy(@Dst.Buff[OldLen], @Src, RoomSize);
	Dst.BuffLen = NewLen;
	Dst.Buff[OldLen + RoomSize] = #0;
	return NewLen;
end

word strbuf.append_cstr(strbuf @Dst; char @Src)
	return strbuf.append_buf(@Dst, @Src, strlen(@Src));
end

word strbuf.append_cstr2(strbuf @Dst; char @Src1; char @Src2)
	strbuf.append_cstr(@Dst, @Src1);
	return strbuf.append_cstr(@Dst, @Src2);
end

word strbuf.append_cstr3(strbuf @Dst; char @Src1; char @Src2; char @Src3)
	strbuf.append_cstr(@Dst, @Src1);
	strbuf.append_cstr(@Dst, @Src2);
	return strbuf.append_cstr(@Dst, @Src3);
end

word strbuf.append_cstr4(strbuf @Dst; char @Src1; char @Src2; char @Src3;
                                      char @Src4)
	strbuf.append_cstr(@Dst, @Src1);
	strbuf.append_cstr(@Dst, @Src2);
	strbuf.append_cstr(@Dst, @Src3);
	return strbuf.append_cstr(@Dst, @Src4);
end

word strbuf.append_cstr5(strbuf @Dst; char @Src1; char @Src2; char @Src3;
                                      char @Src4; char @Src5)
	strbuf.append_cstr(@Dst, @Src1);
	strbuf.append_cstr(@Dst, @Src2);
	strbuf.append_cstr(@Dst, @Src3);
	strbuf.append_cstr(@Dst, @Src4);
	return strbuf.append_cstr(@Dst, @Src5);
end

word strbuf.append_cstr6(strbuf @Dst; char @Src1; char @Src2; char @Src3;
                                      char @Src4; char @Src5; char @Src6)
	strbuf.append_cstr(@Dst, @Src1);
	strbuf.append_cstr(@Dst, @Src2);
	strbuf.append_cstr(@Dst, @Src3);
	strbuf.append_cstr(@Dst, @Src4);
	strbuf.append_cstr(@Dst, @Src5);
	return strbuf.append_cstr(@Dst, @Src6);
end

