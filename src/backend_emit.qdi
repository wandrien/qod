
/*
	Базовые функции для генерирования выходного файла.

*/

/*****************************************************************************/

void FlushOutput()
	if write(hText, @Text, nText) != nText then
		Stop(@L10n.eWRITEERR);
	end:if

	nText=0;
end

/*****************************************************************************/

void OpenOutput(char@ FileName)
	hText = create(@FileName);
	nText = 0;
end

/*****************************************************************************/

void CloseOutput()
	if nText > 0 then
		FlushOutput();
	end:if

	close(hText);
end

void EmitChar_NoRedirection_(char C)
	if nText > nTEXT then
		StopInternal(__FILE__, __LINE__);
	end

	if nText == nTEXT then
		FlushOutput();
	end

	Text[nText] = C;
	inc  nText;
end

/*****************************************************************************/

word EmitDisabled = 0;

void DisableEmit()
	inc EmitDisabled;
end

void EnableEmit()
	dec EmitDisabled;
end

bool ColdRedirectionEnabled;
char ColdBuffer[nTEXT];
strbuf ColdBuf;
bool ColdBufInitialized = false;

/*****************************************************************************/

void Emit_StartColdRedirection() of
	when ColdRedirectionEnabled:
		StopInternal(__FILE__, __LINE__);
	ColdRedirectionEnabled = true;
	if !ColdBufInitialized then
		strbuf.init(@ColdBuf, @ColdBuffer, countof(ColdBuffer));
		ColdBufInitialized = true;
	end:if
end

void Emit_StopColdRedirection() of
	when !ColdRedirectionEnabled:
		StopInternal(__FILE__, __LINE__);
	when strbuf.get_virtual_len(@ColdBuf) > strbuf.get_size(@ColdBuf):
		StopInternal(__FILE__, __LINE__); /* Overflow */
	ColdRedirectionEnabled = false;
end

bool Emit_ColdRedirectionActive() of
	return ColdRedirectionEnabled;
end

bool Emit_ColdRedirectionAllowed() of
	if !ColdBufInitialized then
		strbuf.init(@ColdBuf, @ColdBuffer, countof(ColdBuffer));
		ColdBufInitialized = true;
	end:if
	return strbuf.get_virtual_len(@ColdBuf) * 2 < strbuf.get_size(@ColdBuf);
end

void Emit_DumpColdBuffer() of
	when !ColdBufInitialized:
		return;

	when ColdRedirectionEnabled:
		StopInternal(__FILE__, __LINE__);
	when strbuf.get_virtual_len(@ColdBuf) > strbuf.get_size(@ColdBuf):
		StopInternal(__FILE__, __LINE__); /* Overflow */

	word Len = strbuf.get_actual_len(@ColdBuf);
	word I = 0;
	while I < Len loop
		EmitChar_NoRedirection_(ColdBuffer[I]);
		inc I;
	end:while
	strbuf.truncate(@ColdBuf, 0);
end

/*****************************************************************************/

void EmitChar_ColdRedirection_(char C)
	strbuf.append_char(@ColdBuf, C);
end

/*****************************************************************************/

/* Вывести в листинг символ. */
void EmitChar_(char C)
	if ColdRedirectionEnabled then
		EmitChar_ColdRedirection_(C);
	else
		EmitChar_NoRedirection_(C);
	end:if
end

void EmitChar(char C)
	when EmitDisabled > 0:
		return;

	EmitChar_(C);
end

/*****************************************************************************/

/* Вывести в листинг строку. */
void EmitString(char @S)
	when EmitDisabled > 0:
		return;

	word I = 0;
	while S[I] != #0 do
		EmitChar_(S[I]);
		inc I;
	end
end

/*****************************************************************************/

void EmitStringIgnoreNewLines(char @S)
	when EmitDisabled > 0:
		return;

	word I = 0;
	while S[I] != #0 do
		char C = S[I];
		switch C of
		case '~r', '~n':
			EmitChar_(' ');
		default:
			EmitChar_(C);
		end:switch
		inc I;
	end
end

/*****************************************************************************/

/* Вывести в листинг переход на новую линию. */
void EmitNL()
	EmitString("~r~n");
end

/*****************************************************************************/

/* Вывести в листинг строку и следом переход на новую линию. */
void EmitStringNL(char @Inst)
	EmitString(@Inst);
	EmitNL();
end

/*****************************************************************************/

define PHO_Mode_Auto 0
define PHO_Mode_Skip 1
define PHO_Mode_Invalidate 2

/*****************************************************************************/

/* Если Label не равно 0, выводит метку Label. Следом выводит строку Inst и опциональный комментарий Comment. */
void EmitLine_(LABEL @Label; char @Inst; char @Comment; word PhoMode)
	when EmitDisabled > 0:
		return;

	when @Inst == NULL:
		@Inst = "";
	when @Comment == NULL:
		@Comment = "";

	select
	case @Label != NULL | PhoMode == PHO_Mode_Invalidate:
		PHO_InvalidateRegs();
	case PhoMode == PHO_Mode_Auto:
		@Inst = @PHO_Optimize(@Inst);
	case PhoMode == PHO_Mode_Skip:
		pass;
	default:
		StopInternalCodegen(__FILE__, __LINE__);
	end:select

	when @Label == NULL & Inst[0] == #0 & Comment[0] == #0:
		return;

	when Inst[0] != #0 | @Label != NULL:
		MeaningfulFlags = 0;

	word C = 0;

	if @Label != NULL then
		EmitChar_('@'); inc C;

		char @P = @str(LabelGetValue(@Label));
		word I = 0;
		while P[I] != #0 do
			EmitChar_(P[I]); inc C;
			inc I;
		end:while

		EmitChar_(':'); inc C;
		EmitChar_(' '); inc C;
	end:if

	if Inst[0] != #0 then
		word I = 0;
		while Inst[I] != #0 do
			char Ch = Inst[I];

			switch Ch of
			case '~r', '~n':
				C = 0;
			default:
				while C < 8 do
					EmitChar_(' '); inc C;
				end:while
				inc C;
			end:switch

			EmitChar_(Ch);
			inc I;
		end
	end:if

	if Comment[0] != #0 then
		while C < 40 do
			EmitChar_(' '); inc C;
		end:while
		EmitString(" ; ");
		EmitStringIgnoreNewLines(@Comment);
	end:if

	EmitNL();
end

/*****************************************************************************/

void Emit(char @Inst)
	EmitLine_(NULL, @Inst, NULL, PHO_Mode_Auto);
end

void EmitComment(char @Comment)
	EmitLine_(NULL, NULL, @Comment, PHO_Mode_Skip);
end

void EmitWithPhoMode(char @Inst; word PhoMode)
	EmitLine_(NULL, @Inst, NULL, PhoMode);
end

void EmitPhoInvalidate(char @Inst)
	EmitLine_(NULL, @Inst, NULL, PHO_Mode_Invalidate);
end

void EmitPhoSkip(char @Inst)
	EmitLine_(NULL, @Inst, NULL, PHO_Mode_Skip);
end

void EmitWithPhoModeAndComment(char @Inst; word PhoMode; char @Comment)
	EmitLine_(NULL, @Inst, @Comment, PhoMode);
end

void EmitWithExprComment(char @Inst; char @Comment)
	when !EmitDebugComments_Expr:
		@Comment = NULL;
	EmitLine_(NULL, @Inst, @Comment, PHO_Mode_Auto);
end

/*****************************************************************************/

void EmitLabel(LABEL @Label)
	when @Label == NULL: StopInternal(__FILE__, __LINE__);
	EmitLine_(@Label, "", NULL, PHO_Mode_Auto);
end

void EmitLabelWithComment(LABEL @Label; char @Comment)
	when @Label == NULL: StopInternal(__FILE__, __LINE__);
	EmitLine_(@Label, "", @Comment, PHO_Mode_Auto);
end

void EmitLabelIfUsed(LABEL @Label)
	when LabelIsUsed(@Label):
		EmitLabel(@Label);
end

/*****************************************************************************/

SOURCELOCATION PrevSourceLocation;

void EmitWithSourceNote(char @Inst; word P)
	if P < nNODE & P > 0 & EmitSourceLineNotes then
		char Buff[nBUFF];
		SOURCELOCATION @SourceLocation = @Node[P].SourceLocation;
		if
			(@PrevSourceLocation.FileName != @SourceLocation.FileName |
			PrevSourceLocation.Line != SourceLocation.Line) &
			(int(SourceLocation.Line) != -1)
		then
			strcpy4(@Buff, "#line ", @SourceLocation.FileName, ":", @str(SourceLocation.Line));
			EmitComment(@Buff);
			PrevSourceLocation = SourceLocation;
		end:if
	end:if
	Emit(@Inst);
end

/*****************************************************************************/

char EmitTmpBuff[1024];

void EmitJumpToLabelWithComment(LABEL @Label; char @Comment)
	when !EmitDebugComments_Jumps:
		@Comment = NULL;
	LabelMarkUsed(@Label);
	EmitLine_(NULL, @strcpy2(@EmitTmpBuff, "jmp     @", @str(LabelGetValue(@Label))), @Comment, PHO_Mode_Skip);
end

void EmitJumpToLabel(LABEL @Label)
	EmitJumpToLabelWithComment(@Label, NULL);
end

void EmitBranchToLabel(char @Condition; LABEL @Label)
	char @s = "";
	when Condition[1] == #0: @s = " ";
	LabelMarkUsed(@Label);
	EmitPhoSkip(
		@strcpy5(@EmitTmpBuff, "j", @Condition, @s, "     @", @str(LabelGetValue(@Label)))
	);
end

/*****************************************************************************/
