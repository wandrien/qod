
include "version.qdi";

include "include/hosted.qdi";
include "sys.qdi";

include "compiler_htl_defines.qdi";
include "compiler_htl_structs.qdi";
include "compiler_str.qdi";
include "compiler_config.qdi";
include "compiler_msg.qdi";
include "compiler_chardata.qdi";
include "compiler_input.qdi";
include "compiler_dict.qdi";

include "frontend_config.qdi";
include "backend_config.qdi";

/*****************************************************************************/

/* Вывод отладочных сообщений и сообщений об ошибках. */

void Dbg (char @Msg)
	puts("~r~n");
	puts(@Msg);
	puts("~r~n");
end

/*****************************************************************************/

void PrintVersionAndCopyright()
	puts(@msgPRODUCT);
	puts(" Version ");
	puts(@msgVERSION);
	puts("~r~n");
	puts(@msgCOPYRIGHT);
end

/*****************************************************************************/

void PrintUsage()
	PrintVersionAndCopyright();
	puts(@msgUSAGE);
end

/*****************************************************************************/

include "frontend.qdi";

/*****************************************************************************/

include "backend.qdi";

/*****************************************************************************/

char@ InputFile;
char@ OutputFile;
char@ OutputTreeFile;
char@ OutputIncludeListFile;

char@ DefaultTarget;

/*****************************************************************************/

void Translate()

	/* Синтаксический анализ **********************/

	InitFrontEnd();

	OpenInputFile(@InputFile);

	Parse();

	CloseAllFiles();

	/* Промежуточные операции *********************/

	/* Отметка используемых объектов. */
	DictMarkUsed(pMainFunc);
	select
		case TargetFileFormat == tPE:
			pExitProcess = ResolveInNamespace("ExitProcess", pGlobalNamespace);
			when pExitProcess != nDICT:
				Dict[pExitProcess].Used = 1;
	end:select

	/* Назначаем метки и смещения. */
	word D = AssignLabels_Data();
	AssignLabels_Func();
	AssignLabels_FuncExternal();
	AssignStringOffsets();

	/* Вывод информации по анализу кода ***********/

	when WarnUnusedGlobals != 0:
		DoWarnUnusedGlobals();

	word OutputTreeFile_Done = 0;

	if OutputTreeFile_Done == 0
	 & @OutputTreeFile != NULL
	 & strlen(@OutputTreeFile) != 0
	 & str_ne(@OutputFile, @OutputTreeFile) then
		OutputTreeFile_Done = 1;
		OpenOutput(@OutputTreeFile);
		EmitContentForTreeFile();
		CloseOutput();
	end:if

	if @OutputIncludeListFile != NULL & strlen(@OutputIncludeListFile) != 0 then
		OpenOutput(@OutputIncludeListFile);
		EmitIncludeList();
		CloseOutput();
	end:if

	/* Остановка при отложенных ошибках *****************************/
	when HasErrors:
		halt(1);

	/* Генерация кода *****************************/

	PrepareMulTricks();

	OpenOutput(@OutputFile);

	if OutputTreeFile_Done == 0
	 & @OutputTreeFile != NULL
	 & strlen(@OutputTreeFile) != 0
	 & str_eq(@OutputFile, @OutputTreeFile) then
		OutputTreeFile_Done = 1;
		EmitContentForTreeFile();
	end:if

	char Buff[nBUFF];

	InitAsmDriver();
	EmitFileHeader(@Buff);
	EmitSection_ROData(@Buff);
	EmitSection_Code(@Buff);
	EmitSection_Data(@Buff, D);
	EmitSection_Bss(@Buff, D);

	select
	case TargetFileFormat == tPE:
		EmitPEImport(@Buff);
	end:select

	EmitNL();
	EmitFileFooter(@Buff);

	CloseOutput();

	ShowParseProgress(1);

end

/*****************************************************************************/

char@ GetArg(word I; char@ ErrMsg)

	if I >= Argc then
		if @ErrMsg == NULL then
			StopInternal(__FILE__, __LINE__);
		else
			Stop(@ErrMsg);
		end
	end:if

	return @Argv[I].Arg;
end

/*****************************************************************************/

char@ GetArgNext(word@ I; char@ ErrMsg)
	inc I;
	return @GetArg(I, @ErrMsg);
end

/*****************************************************************************/

void SetupTarget(char@ Target)
	select
	case str_eq(@Target, "--linux"  ):
		TargetFileFormat = tELF;
		TargetMode = mNONE;
	case str_eq(@Target, "--win32-c"):
		TargetFileFormat = tPE;
		TargetMode = mCON;
	case str_eq(@Target, "--win32-w"):
		TargetFileFormat = tPE;
		TargetMode = mGUI;
	default:
		StopInternal(__FILE__, __LINE__);
	end:select
end

/*****************************************************************************/

void SetupOptimization(char@ Mode)
	select
	case str_eq(@Mode, "size"):
		RODataMergeMode = RODataMergeModeAgressive;
		PHOptimization  = true;
		EmitEnterInstruction = true;
		EmitLeaveInstruction = true;
		EmitShortCode = true;
		EmitFastMult = false;
		ReorderOperands = true;
	case str_eq(@Mode, "speed"):
		RODataMergeMode = RODataMergeModeNormal;
		PHOptimization  = true;
		EmitEnterInstruction = false;
		EmitLeaveInstruction = true;
		EmitFastMult = true;
		EmitShortCode = false;
		ReorderOperands = true;
	default:
		RODataMergeMode = RODataMergeModeNone;
		PHOptimization  = false;
		EmitEnterInstruction = false;
		EmitLeaveInstruction = true;
		EmitFastMult = false;
		EmitShortCode = false;
		ReorderOperands = true;
	end:select
end

/*****************************************************************************/

void SetupAssembler(char@ Mode)
	select
	case str_eq(@Mode, "fasm"):
		TargetAssembler = aFASM;
	case str_eq(@Mode, "nasm"):
		TargetAssembler = aNASM;
	default:
		Stop(@eWRONGOPTION);
	end:select
end

/*****************************************************************************/

void __main()

	SetupTarget(@DefaultTarget);
	SetupOptimization("none");

	word i = 1;
	while i < Argc do
		char@ arg = @GetArg(i, NULL);
		select
		case str_eq(@arg, "--help"   ):
			PrintUsage();
			return;
		case str_eq(@arg, "--version"):
			PrintVersionAndCopyright();
			return;
		case str_eq(@arg, "--linux"  )
		   | str_eq(@arg, "--win32-c")
		   | str_eq(@arg, "--win32-w"):
			SetupTarget(@arg);
		case str_eq(@arg, "--output" ):
			@OutputFile = @GetArgNext(@i, @eFNAMEEXP);
		case str_eq(@arg, "--output-tree" ):
			@OutputTreeFile = @GetArgNext(@i, @eFNAMEEXP);
		case str_eq(@arg, "--output-include-list" ):
			@OutputIncludeListFile = @GetArgNext(@i, @eFNAMEEXP);
		case str_eq(@arg, "--asm" ):
			SetupAssembler(@GetArgNext(@i, @eCMDLINEPARAM));
		case str_eq(@arg, "--file" ):
			@InputFile = @GetArgNext(@i, @eFNAMEEXP);
		case str_eq(@arg, "--show-parse-progress" ):
			show_parse_progress = 1;
		case str_eq(@arg, "--optimize" ):
			SetupOptimization(@GetArgNext(@i, @eCMDLINEPARAM));
		case str_eq(@arg, "--warn-unused-globals" ):
			WarnUnusedGlobals = 1;
		case str_eq(@arg, "--error-deprecated-features" ):
			ErrorDeprecatedFeatures = 1;
		case str_eq(@arg, "--no-error-deprecated-features" ):
			ErrorDeprecatedFeatures = 0;
		case str_eq(@arg, "--emit-source-line-notes" ):
			EmitSourceLineNotes = 1;
		case str_has_prefix(@arg, "-"):
			Stop(@eWRONGOPTION);
		default:
			@InputFile = @GetArg(i, @eFNAMEEXP);
		end:select
		inc i;
	end:while

	if @InputFile == NULL then
		PrintUsage();
		return;
	end

	char Name[nBUFF];

	if @OutputFile == NULL then
		word l = strlen(@InputFile);
		if l + 4 >= nBUFF then
			Stop(@eLONGFILENAME);
		end:if
		strcat(@strcpy(@Name, @InputFile), ".asm");
		@OutputFile = @Name;
	end:if

	Translate();
end

/*****************************************************************************/

